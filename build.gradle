plugins {
    id 'org.ajoberstar.grgit' version '4.0.2' apply false
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'com.baidu.jprotobuf' version "1.1.1" apply false
}

apply from: "${rootDir}/gradle/dependency.gradle"
apply from: "${rootDir}/gradle/git.gradle"

ext {
    moduleProjects = subprojects.findAll { it.name.startsWith("tny-game-") }
    moduleProjects = moduleProjects - project(":tny-game-bom")
    javaProjects = moduleProjects - project(":tny-game-bom")
    javaProjects = javaProjects.findAll { !it.name.endsWith("-gradle") }
    gradleProjects = moduleProjects.findAll { it.name.endsWith("-gradle") }
//    moduleProjects = moduleProjects - project(":tny-game-bom")
}

configure(allprojects) {
    apply plugin: "idea"
    apply plugin: 'maven-publish'
    apply plugin: "io.spring.dependency-management"

    group 'com.tny.game'
    version "${projectVersion}"


    dependencyManagement {

        imports {
            mavenBom(libs.jackson_bom)
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${alibabaCloudVersion}"
            mavenBom("org.apache.logging.log4j:log4j-bom:${log4j2Version}")

        }

        dependencies {
            dependency libs.commons_codec
            dependency libs.commons_beanutils
            dependency libs.commons_collections
            dependency libs.commons_collections4
            dependency libs.commons_configuration
            dependency libs.commons_lang
            dependency libs.commons_lang3
            dependency libs.commons_pool2
            dependency libs.javassist
            dependency libs.guava
            dependency libs.okio
            dependency libs.okhttp
            dependency libs.okhttp3
            dependency libs.asm
        }

        generatedPomCustomization {
            enabled = false
        }
    }

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://maven.aliyun.com/repository/spring" }
        maven { url "https://m2.tnydev.com/repository/maven-public/" }
        maven { url "https://maven.aliyun.com/repository/central" }
        maven { url "https://maven.aliyun.com/repository/google" }
        maven { url "https://maven.aliyun.com/repository/jcenter" }
    }


}

configure(gradleProjects) {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply from: "${rootDir}/gradle/publications.gradle"

    configurations {
        all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    [compileJava, compileGroovy, javadoc]*.options*.encoding = "${encoding}"

    dependencies {
        api gradleApi()
        api localGroovy()
//            compile 'com.intellij:forms_rt:7.0.3'
//            compile 'org.hidetake:gradle-ssh-plugin:2.9.0'
//            compile 'org.ajoberstar:grgit:2.2.0'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    task createProject {
        doLast {
            sourceSets*.groovy.srcDirs*.each { it.mkdirs() }
            sourceSets*.resources.srcDirs*.each { it.mkdirs() }
        }
    }
}

configure(javaProjects) {

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply from: "${rootDir}/gradle/publications.gradle"
    apply from: "${rootDir}/gradle/tny-module.gradle"

    ext.springBoot = name.endsWith("-demo") || name.endsWith("-suite") || name.contains("-starter-")

    sourceCompatibility = "${javaVersion}"
    targetCompatibility = "${javaVersion}"

    task createProject {
        doLast {
            sourceSets*.java.srcDirs*.each { it.mkdirs() }
            sourceSets*.resources.srcDirs*.each { it.mkdirs() }
        }
    }

    configurations {
        all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
        compile.exclude module: 'spring-boot-starter-logging'//排除对默认logging的依赖
    }

    compileJava.dependsOn(processResources)
    [compileJava, compileTestJava, javadoc]*.options*.encoding = "${encoding}"

    compileJava {
        options.compilerArgs << '-parameters'
        options.fork = true
        options.encoding = "UTF-8"
    }

    jar {
        exclude("**.sql", "**.xml", "**.properties")
    }

    test {
        useJUnitPlatform()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    java {
        withSourcesJar()
    }

    dependencies {

        implementation libs.slf4j_api

        testImplementation libs.mockito_junit_jupiter
        testImplementation libs.junit_jupiter
//        testImplementation libs.junit_jupiter_engine
        testImplementation libs.jmock_junit5
        testImplementation libs.spring_test
        testImplementation libs.slf4j_simple
        testImplementation project(":tny-game-tester")
    }


    idea {
        module {
            inheritOutputDirs = false
            outputDir = compileJava.destinationDir
            testOutputDir = compileTestJava.destinationDir
            downloadJavadoc = false
            downloadSources = false
        }
    }


}